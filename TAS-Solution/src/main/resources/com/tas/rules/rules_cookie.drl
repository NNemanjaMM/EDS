//created on: Jun 6, 2017
package com.tas.rules

//list any import classes here.
import com.tas.codes.AssetCode;
import com.tas.codes.VulnerabilityCode;
import com.tas.model.risk_pattern.*;
import com.tas.model.diagram.Process;
import com.tas.model.diagram.Element;
import com.tas.model.diagram.*;
import java.util.List;

//declare any global variables here


rule "Asset:_Session_Cookie"
    when
     	DiagramPattern($list : assets)  
     	$value : Assets.Asset() from $list  
     	Boolean(booleanValue == true) from $value.assetId == AssetCode.SESSION_COOKIE               
    then
        drools.setFocus("sc_check_destination");
end


rule "SC:_Check_process"
	agenda-group "sc_check_destination"
    when
        component: DiagramPattern(element instanceof Process)                    
    then
        drools.setFocus("sc_process");
end


rule "SC:_Check_external"
	agenda-group "sc_check_destination"
    when
        component: DiagramPattern(element instanceof ExternalEntity)                    
    then
        drools.setFocus("sc_external");
end


rule "SC:_Check_datastore"
	agenda-group "sc_check_destination"
    when
        component: DiagramPattern(element instanceof DataStore)                    
    then
        drools.setFocus("sc_datastore");
end


rule "SC_External:_Process"
	agenda-group "sc_external"
    when    
        DiagramPattern(traceStart instanceof Process)      
    then
        drools.setFocus("sc_external_from_process");
end


rule "SC_External_from_Process:_XSS_1"
	agenda-group "sc_external_from_process"
    when
     	pattern : DiagramPattern(traceStart instanceof Process)
     	DiagramPattern($list : trace)
     	elementInTrace : Element() from $list
     	Boolean(booleanValue == true) from elementInTrace instanceof Process  
     	Boolean(booleanValue == true) from ((Process)elementInTrace).sanitizeOutput == false
    then
        pattern.addVulnerability(VulnerabilityCode.XSS);
end

